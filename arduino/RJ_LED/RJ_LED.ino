// A basic everyday NeoPixel strip test program.

// NEOPIXEL BEST PRACTICES for most reliable operation:
// - Add 1000 uF CAPACITOR between NeoPixel strip's + and - connections.
// - MINIMIZE WIRING LENGTH between microcontroller board and first pixel.
// - NeoPixel strip's DATA-IN should pass through a 300-500 OHM RESISTOR.
// - AVOID connecting NeoPixels on a LIVE CIRCUIT. If you must, ALWAYS
//   connect GROUND (-) first, then +, then data.
// - When using a 3.3V microcontroller with a 5V-powered NeoPixel strip,
//   a LOGIC-LEVEL CONVERTER on the data line is STRONGLY RECOMMENDED.
// (Skipping these may work OK on your workbench but can fail in the field)

#include <Adafruit_NeoPixel.h>
#ifdef __AVR__
 #include <avr/power.h> // Required for 16 MHz Adafruit Trinket
#endif

// Which pin on the Arduino is connected to the NeoPixels?
// On a Trinket or Gemma we suggest changing this to 1:
#define LED_PIN    9

// How many NeoPixels are attached to the Arduino?
#define LED_COUNT 49

// Declare our NeoPixel strip object:
Adafruit_NeoPixel strip(LED_COUNT, LED_PIN, NEO_GRB + NEO_KHZ800);
// Argument 1 = Number of pixels in NeoPixel strip
// Argument 2 = Arduino pin number (most are valid)
// Argument 3 = Pixel type flags, add together as needed:
//   NEO_KHZ800  800 KHz bitstream (most NeoPixel products w/WS2812 LEDs)
//   NEO_KHZ400  400 KHz (classic 'v1' (not v2) FLORA pixels, WS2811 drivers)
//   NEO_GRB     Pixels are wired for GRB bitstream (most NeoPixel products)
//   NEO_RGB     Pixels are wired for RGB bitstream (v1 FLORA pixels, not v2)
//   NEO_RGBW    Pixels are wired for RGBW bitstream (NeoPixel RGBW products)






// setup() function -- runs once at startup --------------------------------

void setup() {
  // These lines are specifically to support the Adafruit Trinket 5V 16 MHz.
  // Any other board, you can remove this part (but no harm leaving it):
#if defined(__AVR_ATtiny85__) && (F_CPU == 16000000)
  clock_prescale_set(clock_div_1);
#endif
  // END of Trinket-specific code.

  strip.begin();           // INITIALIZE NeoPixel strip object (REQUIRED)
  strip.show();            // Turn OFF all pixels ASAP
  strip.setBrightness(50); // Set BRIGHTNESS to about 1/5 (max = 255)


  // Serial Port
  Serial.begin(115200);
}


// loop() function -- runs repeatedly as long as board is on ---------------

// ------------------------- Robot command ----------------------------------
int cmd = 0;
int mode = 3, p1=0, p2=255, p3=80, p4=100;

// ------------------------- SERIAL -----------------------------------------
const byte numChars = 32;
char receivedChars[numChars];
boolean newData = false;

void recvWithStartEndMarkers() {
  static boolean recvInProgress = false;
  static byte ndx = 0;
  char startMarker = '$';
  char endMarker = '\n';
  char rc;

  while (Serial.available() > 0 && newData == false) {
    rc = Serial.read();

    if (recvInProgress == true) {
      if (rc != endMarker) {
        receivedChars[ndx] = rc;
        ndx++;
        if (ndx >= numChars) {
          ndx = numChars - 1;
        }
      }
      else {
        receivedChars[ndx] = '\0'; // terminate the string
        recvInProgress = false;
        ndx = 0;
        newData = true;
      }
    }

    else if (rc == startMarker) {
      recvInProgress = true;
    }
  }
}

void parseData() {
  // Temp String
  char tempChars[numChars];
  strcpy(tempChars, receivedChars);

  // Token Pointer
  char * strtokIndx;

  // Parse Each Token
  strtokIndx = strtok(tempChars,",");
  cmd = atoi(strtokIndx);

  strtokIndx = strtok(NULL, ",");
  p1 = atoi(strtokIndx);

  strtokIndx = strtok(NULL, ",");
  p2 = atoi(strtokIndx);

  strtokIndx = strtok(NULL, ",");
  p3 = atoi(strtokIndx);

  strtokIndx = strtok(NULL, ",");
  p4 = atoi(strtokIndx);
}

void showParsedData() {
  Serial.print("Command: ");
  Serial.print(cmd);
  Serial.print(",");
  Serial.print(p1);
  Serial.print(",");
  Serial.print(p2);
  Serial.print(",");
  Serial.print(p3);
  Serial.print(",");
  Serial.print(p4);
  Serial.print('\n');
}
// ------------------------------------------------------------------


void loop() {  
  recvWithStartEndMarkers();
  if(newData) {
    parseData();
    showParsedData();
    newData = false;
  }


 if (cmd < 1)
 {
  //NO OP
 }
 else if (cmd < 90)
 {
  mode = cmd;
 }
 else if (cmd == 90)
 {
   strip.setBrightness(p1);
   cmd = 0;
 }
 else
 {
   cmd = 0;
 }
  

  // Fill along the length of the strip in various colors...
  switch(mode)
  {
    
    case 1:
    colorWipe(strip.Color(p1, p2, p3), p4);
    break;
    
    case 2:
    theaterChase(strip.Color(p1, p2, p3), p4);
    break;

    case 3:
    rainbow(p1);
    break;
    
    case 4:
    theaterChaseRainbow(p1);
    break;

    case 5:
    Pong(strip.Color(p1, p2, p3), p4);
    break;

    case 6:
    theaterChaseBackward(strip.Color(p1, p2, p3), p4);
    break;
  }
}

void Pong(uint32_t color, int wait) {
  static int start = 10;
  int len = 4;
  static boolean reverse = false;
  
  for(int i=0; i<strip.numPixels(); i++) { // For each pixel in strip...
    if (i >= start && i <= (start + len)){
      strip.setPixelColor(i, color);
    }
    else {
      strip.setPixelColor(i, strip.Color(0, 0, 0));
      }         //  Set pixel's color (in RAM)
  }
    strip.show();                          //  Update strip to match
    delay(wait);
    if (reverse == false)
    {
      start++;
    }
    else 
    {
      start--;
    }
    if (start + len == strip.numPixels())
    {
      reverse = true;
      }
    if (start == 0)
    {
      reverse = false;
    }
}

// Some functions of our own for creating animated effects -----------------

// Fill strip pixels one after another with a color. Strip is NOT cleared
// first; anything there will be covered pixel by pixel. Pass in color
// (as a single 'packed' 32-bit value, which you can get by calling
// strip.Color(red, green, blue) as shown in the loop() function above),
// and a delay time (in milliseconds) between pixels.
void colorWipe(uint32_t color, int wait) {
  for(int i=0; i<strip.numPixels(); i++) { // For each pixel in strip...
    strip.setPixelColor(i, color);         //  Set pixel's color (in RAM)
    strip.show();                          //  Update strip to match
    delay(wait);                           //  Pause for a moment
  }
}

// Theater-marquee-style chasing lights. Pass in a color (32-bit value,
// a la strip.Color(r,g,b) as mentioned above), and a delay time (in ms)
// between frames.
void theaterChase(uint32_t color, int wait) {
    for(int b=0; b<3; b++) { //  'b' counts from 0 to 2...
      strip.clear();         //   Set all pixels in RAM to 0 (off)
      // 'c' counts up from 'b' to end of strip in steps of 3...
      for(int c=b; c<strip.numPixels(); c += 3) {
        strip.setPixelColor(c, color); // Set pixel 'c' to value 'color'
      }
      strip.show(); // Update strip with new contents
      delay(wait);  // Pause for a moment
    }
}

void theaterChaseBackward(uint32_t color, uint8_t wait) { 
    for (int b=0; b < 3; b++) {
    strip.clear();
      for (int c=b; c < strip.numPixels(); c+=3) {
        strip.setPixelColor(c+b, color);    //turn every third pixel on
      }
      strip.show();
      delay(wait);
      for (int c=b; c < strip.numPixels(); c+=3) {
        strip.setPixelColor(c+b, 0);        //turn every third pixel off
      }
 
    }
  
}

// Rainbow cycle along whole strip. Pass delay time (in ms) between frames.
void rainbow(int wait) {
  // Hue of first pixel runs 5 complete loops through the color wheel.
  // Color wheel has a range of 65536 but it's OK if we roll over, so
  // just count from 0 to 5*65536. Adding 256 to firstPixelHue each time
  // means we'll make 5*65536/256 = 1280 passes through this loop:
  
  static long firstPixelHue = 0; 
  if(firstPixelHue > 5*65536) 
  {
    firstPixelHue = 0; 
  }
  
  //for(long firstPixelHue = 0; firstPixelHue < 5*65536; firstPixelHue += 256) {
    // strip.rainbow() can take a single argument (first pixel hue) or
    // optionally a few extras: number of rainbow repetitions (default 1),
    // saturation and value (brightness) (both 0-255, similar to the
    // ColorHSV() function, default 255), and a true/false flag for whether
    // to apply gamma correction to provide 'truer' colors (default true).
    strip.rainbow(firstPixelHue);
    // Above line is equivalent to:
    // strip.rainbow(firstPixelHue, 1, 255, 255, true);
    strip.show(); // Update strip with new contents
    delay(wait);  // Pause for a moment
  //}
  
  firstPixelHue += 256;
}

// Rainbow-enhanced theater marquee. Pass delay time (in ms) between frames.
void theaterChaseRainbow(int wait) {
  int firstPixelHue = 0;     // First pixel starts at red (hue 0)

  static int b=0; 
  if(b>3){
    b=0;
  }
  
 //   for(int b=0; b<3; b++) { //  'b' counts from 0 to 2...
      strip.clear();         //   Set all pixels in RAM to 0 (off)
      // 'c' counts up from 'b' to end of strip in increments of 3...
      for(int c=b; c<strip.numPixels(); c += 3) {
        // hue of pixel 'c' is offset by an amount to make one full
        // revolution of the color wheel (range 65536) along the length
        // of the strip (strip.numPixels() steps):
        int      hue   = firstPixelHue + c * 65536L / strip.numPixels();
        uint32_t color = strip.gamma32(strip.ColorHSV(hue)); // hue -> RGB
        strip.setPixelColor(c, color); // Set pixel 'c' to value 'color'
      }
      strip.show();                // Update strip with new contents
      delay(wait);                 // Pause for a moment
      firstPixelHue += 65536 / 90; // One cycle of color wheel over 90 frames
 //   }

  b++;
}
